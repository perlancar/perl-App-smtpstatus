#!perl

# DATE
# VERSION

use 5.010001;
use strict;
use warnings;
use Perinci::CmdLine::Any;

our %SPEC;

$SPEC{smtpstatus} = {
    v => 1.1,
    summary => 'List SMTP statuses',
    args => {
        query => {
            summary     => 'Search query',
            schema      => ['str*'],
            description => <<'_',

To search for a specific code: `4.` or `x.1` or `.1.4`.

To search in summary or description: `bad` or `bad dest`.

_
            pos         => 0,
        },
        description => {
            summary     => 'Whether to show description',
            schema      => [bool => default => 0],
        },
    },
};
sub smtpstatus {
    require App::smtpstatus;

    my %args = @_;
    my $q = $args{query};

    my %fa;
    $fa{with_field_names} = 1;
    $fa{fields} = ['code', 'summary'];
    push @{ $fa{fields} }, 'description' if $args{description};
    if (defined $q) {
        if ($q =~ /^\.?(\d+|x)(?:\.(\d+|x))*$/i) {
            $fa{'code.contains'} = uc($q);
        } else {
            $fa{query} = $q;
        }
    }
    #use Data::Dump; dd \%fa;
    App::smtpstatus::list_smtp_statuses(%fa);
}

Perinci::CmdLine::Any->new(url => '/main/smtpstatus')->run;

1;
# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

 # Show all statuses
 % smtpstatus

 # Show all X.1.X statuses
 % smtpstatus x.1

 # Show all statuses containing 'bad destination' in summary/description
 % smtpstatus 'bad dest'

 # Show longer description
 % smptatus 'search string' --desc
