#!/usr/bin/perl

use 5.010001;
use strict;
use warnings;
use Perinci::CmdLine 0.68;

# VERSION

our %SPEC;

$SPEC{smtpstatus} = {
    v => 1.1,
    summary => 'List SMTP statuses',
    args => {
        query => {
            summary     => 'Search query',
            schema      => ['str*'],
            description => <<'_',

To search for a specific code: `4.` or `x.1` or `.1.4`.

To search in summary or description: `bad` or `bad dest`.

_
            pos         => 0,
        },
        description => {
            summary     => 'Whether to show description',
            schema      => [bool => default => 0],
        },
    },
};
sub smtpstatus {
    require App::smtpstatus;

    my %args = @_;
    my $q = $args{query};

    my %fa;
    $fa{with_field_names} = 1;
    $fa{fields} = ['code', 'summary'];
    push @{ $fa{fields} }, 'description' if $args{description};
    if (defined $q) {
        if ($q =~ /^\.?(\d+|x)(?:\.(\d+|x))*$/i) {
            $fa{'code.contains'} = uc($q);
        } else {
            $fa{q} = $q;
        }
    }
    #use Data::Dump; dd \%fa;
    App::smtpstatus::list_smtp_statuses(%fa);
}

Perinci::CmdLine->new(url => '/main/smtpstatus')->run;

1;
# ABSTRACT: List SMTP statuses
# PODNAME: smtpstatus

=head1 SYNOPSIS

 # Show all statuses
 % smtpstatus

 # Show all X.1.X statuses
 % smtpstatus x.1

 # Show all statuses containing 'bad destination' in summary/description
 % smtpstatus 'bad dest'

 # Show longer description
 % smptatus 'search string' --desc


=head1 FAQ

=head2 Lots of dependencies?

Yup, sorry about that. I use L<Perinci::CmdLine> and
L<Perinci::Sub::Gen::AccessTable> for convenience, where I just specify a data
structure and let the aforementioned modules generate an access function and
command-line interface for it.


=head1 TODO

=over

=item * Add implementation-specific codes (from Sendmail, qmail, Postfix, Exim, Spanel)

=item * Format

Use --format=text-pretty by default for interactive, set description's
table_column_orders (after summary, not before) and table_column_formats ([[wrap
=> {columns=>40}]]).

=back

=cut
